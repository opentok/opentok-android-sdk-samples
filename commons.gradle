import groovy.io.FileType

ext {
    extAgpVersion="4.1.1"
    extCompileSdkVersion=30
    extBuildToolsVersion="30.0.2"
    extMinSdkVersion=16
    extTargetSdkVersion=28
    extVersionCode=1
    extVersionName="1.0"
    extMinifyEnabled=false
    extAppCompatVersion="1.1.0"
    extOpentokSdkVersion="2.18.+"
}

/* 
Look for the `OpenTokConfig` file in the project and checks if 
API_KEY, SESSION_ID and TOKEN are empty. This check prevents from committing credentials.
*/
task checkOpenTokConfig() {
    doLast {
        def openTokConfigFound = false

        def dir = new File(".")
        dir.eachFileRecurse (FileType.FILES) { file ->

            if(file.name == "OpenTokConfig.java") {
                openTokConfigFound = true

                checkOpenTokConfigProperty(file, "API_KEY")
                checkOpenTokConfigProperty(file, "SESSION_ID")
                checkOpenTokConfigProperty(file, "TOKEN")
            }
        }

        if (!openTokConfigFound) {
            throw new Exception("OpenTokConfig.java not found")
        }
    }
}

def checkOpenTokConfigProperty(file, propertyName) {
    file.eachLine { line ->
        // If property exists withing config file it must be empty
        if (line.contains("public static final String $propertyName") && !line.contains('""')) {
            throw new Exception("Opentok config property is not empty: ${line}")
        }
    }
}